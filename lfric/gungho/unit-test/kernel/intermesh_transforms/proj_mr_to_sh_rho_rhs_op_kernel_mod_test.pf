!-----------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> Test the proj_mr_to_sh_rho_rhs_op kernel
!>
module proj_mr_to_sh_rho_rhs_op_kernel_mod_test

  use constants_mod,                       only : i_def, r_def
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w0_m3x3_q3x3x3_size, &
                                                  get_w3_m3x3_q3x3x3_size, &
                                                  get_wtheta_m3x3_q3x3x3_size
  use get_unit_test_m3x3_dofmap_mod,       only : get_w0_m3x3_dofmap, &
                                                  get_w3_m3x3_dofmap, &
                                                  get_wtheta_m3x3_dofmap
  use get_unit_test_q3x3x3_basis_mod,      only : get_w0_q3x3x3_diff_basis, &
                                                  get_w0_q3x3x3_basis,      &
                                                  get_w3_q3x3x3_basis,      &
                                                  get_wtheta_q3x3x3_basis
  use get_unit_test_q3x3x3_quadrature_mod, only : get_gaussian_q3x3x3_quadrature_weights_xy, &
                                                  get_gaussian_q3x3x3_quadrature_weights_z
  use pFUnit_Mod

  implicit none

  private

  public :: proj_mr_to_sh_rho_rhs_op_test_type, test_all

  @TestCase
  type, extends(TestCase) :: proj_mr_to_sh_rho_rhs_op_test_type
    private

  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type proj_mr_to_sh_rho_rhs_op_test_type

contains
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : cellshape_quadrilateral, &
                                          coord_system_xyz
    use feign_config_mod,          only : feign_finite_element_config

    implicit none

    class(proj_mr_to_sh_rho_rhs_op_test_type), intent(inout) :: this

    call feign_finite_element_config(           &
             cellshape=cellshape_quadrilateral, &
             coord_order=0_i_def,               &
             coord_system=coord_system_xyz,     &
             element_order=0_i_def,             &
             rehabilitate=.true.,               &
             vorticity_in_w1=.false. )

  end subroutine setUp
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none


    class(proj_mr_to_sh_rho_rhs_op_test_type), intent(inout) :: this

    call final_configuration()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @Test
  subroutine test_all( this)

    use, intrinsic :: iso_fortran_env, only : real64
    use proj_mr_to_sh_rho_rhs_op_kernel_mod, only : proj_mr_to_sh_rho_rhs_op_code

    implicit none

    class(proj_mr_to_sh_rho_rhs_op_test_type), intent(inout) :: this

    real(kind=r_def), parameter :: tol = 1.0e-12_r_def  ! r_def 64bit tolerance
    real(kind=r_def), parameter :: dx = 4.0_r_def, &
                                   dy = 3.0_r_def, &
                                   dz = 2.0_r_def
    real(kind=r_def)    :: use_tol
    real(kind=r_def)    :: I_lower_i_ip1_answer, I_upper_i_i_answer
    real(kind=r_def)    :: I_lower_i_i_answer, I_upper_i_im1_answer
    integer(kind=i_def) :: nlayers, ncells, ncell_3d, cell, icell
    integer(kind=i_def) :: ndf_w3, undf_w3, ndf_pid, undf_pid
    integer(kind=i_def) :: ndf_dl_w0, undf_dl_w0, ndf_wtheta, undf_wtheta
    integer(kind=i_def) :: dim_space, dim_space_diff
    integer(kind=i_def) :: nqp_h, nqp_v
    integer(kind=i_def) :: i, j, k
    integer(kind=i_def), allocatable :: map_dl_w0(:,:)
    integer(kind=i_def), allocatable :: map_w3(:,:)
    integer(kind=i_def), allocatable :: map_pid(:,:)
    integer(kind=i_def), allocatable :: map_wtheta(:,:)
    real(kind=r_def), allocatable    :: panel_id(:)
    real(kind=r_def), allocatable    :: basis_w0(:,:,:,:)
    real(kind=r_def), allocatable    :: diff_basis_w0(:,:,:,:)
    real(kind=r_def), allocatable    :: basis_w3(:,:,:,:)
    real(kind=r_def), allocatable    :: basis_wtheta(:,:,:,:)
    real(kind=r_def), allocatable    :: wh(:), wv(:)
    real(kind=r_def), allocatable    :: dl_chi1(:), dl_chi2(:), dl_chi3(:)
    real(kind=r_def), allocatable    :: I_lower_i_ip1(:)
    real(kind=r_def), allocatable    :: I_upper_i_i(:)
    real(kind=r_def), allocatable    :: I_lower_i_i(:)
    real(kind=r_def), allocatable    :: I_upper_i_im1(:)
    real(kind=r_def), allocatable    :: dummy_theta(:)


    nlayers=3
    call get_w3_m3x3_q3x3x3_size( ndf_w3, undf_w3, ncells,   &
                                  dim_space, dim_space_diff, &
                                  nqp_h, nqp_v,              &
                                  nlayers )
    call get_wtheta_m3x3_q3x3x3_size( ndf_wtheta, undf_wtheta, ncells, &
                                      dim_space, dim_space_diff,       &
                                      nqp_h, nqp_v,                    &
                                      nlayers )
    ndf_dl_w0 = 8
    undf_dl_w0 = 63

    ndf_pid = 1
    undf_pid = ncells

    call get_w0_m3x3_dofmap(map_dl_w0, 6)
    call get_w3_m3x3_dofmap(map_w3)
    call get_w3_m3x3_dofmap(map_pid, 1)
    call get_wtheta_m3x3_dofmap(map_wtheta)
    call get_w0_q3x3x3_basis(basis_w0)
    call get_w0_q3x3x3_diff_basis(diff_basis_w0)
    call get_w3_q3x3x3_basis(basis_w3)
    call get_wtheta_q3x3x3_basis(basis_wtheta)
    call get_gaussian_q3x3x3_quadrature_weights_xy(wh)
    call get_gaussian_q3x3x3_quadrature_weights_z(wv)

    ! Compute coordinates
    allocate(dl_chi1(undf_dl_w0))
    allocate(dl_chi2(undf_dl_w0))
    allocate(dl_chi3(undf_dl_w0))
    allocate(panel_id(ncells))


    icell = 1
    do j = 1,3
      do i = 1,3
        do k = 0,6
          dl_chi1(map_dl_w0(1,icell)+k) = real(i-1)*dx
          dl_chi2(map_dl_w0(1,icell)+k) = real(j-1)*dy
          dl_chi3(map_dl_w0(1,icell)+k) = real(k)*0.5*dz
        end do
        icell = icell + 1
      end do
    end do

    panel_id(:) = 1_r_def

    allocate(dummy_theta(undf_wtheta))
    allocate(I_lower_i_ip1(undf_w3))
    allocate(I_lower_i_i(undf_w3))
    allocate(I_upper_i_i(undf_w3))
    allocate(I_upper_i_im1(undf_w3))

    cell = 1
    call proj_mr_to_sh_rho_rhs_op_code(                                   &
                                        nlayers,                          &
                                        I_lower_i_ip1,                    &
                                        I_lower_i_i,                      &
                                        I_upper_i_i,                      &
                                        I_upper_i_im1,                    &
                                        dl_chi1,                          &
                                        dl_chi2,                          &
                                        dl_chi3,                          &
                                        panel_id,                         &
                                        dummy_theta,                      &
                                        ndf_w3, undf_w3,                  &
                                        map_w3(:,cell),                   &
                                        ndf_dl_w0, undf_dl_w0,            &
                                        map_dl_w0(:,cell),                &
                                        basis_w0, diff_basis_w0,          &
                                        ndf_pid, undf_pid,                &
                                        map_pid(:,cell),                  &
                                        ndf_wtheta, undf_wtheta,          &
                                        map_wtheta(:,cell), basis_wtheta, &
                                        nqp_h, nqp_v, wh, wv              &
                                      )

    I_lower_i_ip1_answer = dx * dy * dz / 8.0
    I_lower_i_i_answer = 3.0 * dx * dy * dz / 8.0
    I_upper_i_i_answer = 3.0 * dx * dy * dz / 8.0
    I_upper_i_im1_answer = dx * dy * dz / 8.0
    @assertEqual(I_lower_i_ip1_answer, I_lower_i_ip1(map_w3(1, cell)), tol)
    @assertEqual(I_lower_i_i_answer,   I_lower_i_i(map_w3(1, cell)),   tol)

    if ( r_def == real64 ) then
      use_tol = tol
    else
      use_tol = 10.0_r_def*spacing( I_upper_i_i(map_w3(1, cell) ))
    end if
    @assertEqual(I_upper_i_i_answer,   I_upper_i_i(map_w3(1, cell)),   use_tol)
    @assertEqual(I_upper_i_im1_answer, I_upper_i_im1(map_w3(1, cell)), use_tol)

    deallocate(map_dl_w0)
    deallocate(map_w3)
    deallocate(map_wtheta)
    deallocate(map_pid)
    deallocate(basis_w0)
    deallocate(diff_basis_w0)
    deallocate(basis_w3)
    deallocate(basis_wtheta)
    deallocate(wh)
    deallocate(wv)
    deallocate(dl_chi1)
    deallocate(dl_chi2)
    deallocate(dl_chi3)
    deallocate(panel_id)
    deallocate(I_upper_i_i)
    deallocate(I_lower_i_i)
    deallocate(I_lower_i_ip1)
    deallocate(I_upper_i_im1)

  end subroutine test_all

end module proj_mr_to_sh_rho_rhs_op_kernel_mod_test
