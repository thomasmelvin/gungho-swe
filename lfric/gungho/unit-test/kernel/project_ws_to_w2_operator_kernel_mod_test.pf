!-----------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the projection from a scalar space to W2
module project_ws_to_w2_operator_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: project_ws_to_w2_operator_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type project_ws_to_w2_operator_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : cellshape_quadrilateral, &
                                          coord_system_xyz
    use feign_config_mod,          only : feign_finite_element_config

    implicit none

    class(project_ws_to_w2_operator_test_type), intent(inout) :: this

    call feign_finite_element_config(           &
             cellshape=cellshape_quadrilateral, &
             coord_order=0_i_def,               &
             element_order=0_i_def,             &
             rehabilitate=.true.,               &
             coord_system=coord_system_xyz,     &
             vorticity_in_w1=.false. )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod, only: final_configuration

    implicit none

    class(project_ws_to_w2_operator_test_type), intent(inout) :: this

    call final_configuration()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @Test
  subroutine test_all( this )

    use project_ws_to_w2_operator_kernel_mod, only : project_ws_to_w2_operator_code

    implicit none

    class(project_ws_to_w2_operator_test_type), intent(inout) :: this

    real(kind=r_def), parameter :: tol = 1.0e-12_r_def

    ! Mesh
    integer(kind=i_def), parameter :: ncell   = 1
    integer(kind=i_def), parameter :: nlayers = 1
    integer(kind=i_def), parameter :: cell    = 1

    ! Array sizes
    integer(kind=i_def), parameter :: ndf_w2 = 4_i_def
    integer(kind=i_def), parameter :: ndf_ws = 1_i_def
    integer(kind=i_def), parameter :: ndf_wx = 8_i_def
    integer(kind=i_def), parameter :: ndf_p  = 1_i_def
    integer(kind=i_def), parameter :: undf_wx = ndf_wx
    integer(kind=i_def), parameter :: undf_p  = ndf_p
    ! Operators to test
    real(kind=r_def), dimension(ndf_w2,ndf_ws,ncell) :: projection_operator
    ! Maps
    integer(kind=i_def), dimension(ndf_wx) :: map_wx
    integer(kind=i_def), dimension(ndf_p)  :: map_p
    ! Basis arrays
    real(kind=r_def), dimension(3,ndf_w2) :: basis_w2
    real(kind=r_def), dimension(1,ndf_ws) :: basis_ws
    real(kind=r_def), dimension(1,ndf_wx) :: basis_wx
    real(kind=r_def), dimension(3,ndf_wx) :: diff_basis_wx
    ! Quadrature
    integer(kind=i_def), parameter   :: nqp = 1_i_def
    real(kind=r_def), dimension(nqp) :: wgt
    ! Fields
    real(kind=r_def), dimension(ndf_p)  :: panel_id
    ! Coordinate fields
    real(kind=r_def), dimension(ndf_wx) :: x, y, z
    real(kind=r_def)                    :: dx, dy, dz, zero, detj
    ! Scalars
    integer(kind=i_def)                 :: direction

    integer(kind=i_def) :: i

    real(kind=r_def), dimension(ndf_w2, ndf_ws, 3) :: answer

    ! Set cell coordinates
    dx = 2.0_r_def
    dy = 3.0_r_def
    dz = 4.0_r_def
    zero = 0.0_r_def
    x = (/ zero, dx,   dx,   zero, zero, dx,   dx, zero /)
    y = (/ zero, zero, dy,   dy,   zero, zero, dy, dy /)
    z = (/ zero, zero, zero, zero, dz,   dz,   dz, dz /)

    do i = 1,ndf_wx
      map_wx(i) = int(i,i_def)
    end do
    do i = 1,ndf_p
      map_p(i) = int(i,i_def)
    end do

    ! Quadrature
    wgt(:) = 1.0_r_def

    ! Basis functions
    basis_w2(:,1) = (/ 0.5_r_def, 0.0_r_def, 0.0_r_def /)
    basis_w2(:,2) = (/ 0.5_r_def, 0.0_r_def, 0.0_r_def /)
    basis_w2(:,3) = (/ 0.0_r_def, 0.0_r_def, 0.5_r_def /)
    basis_w2(:,4) = (/ 0.0_r_def, 0.0_r_def, 0.5_r_def /)

    basis_ws(1,1) = 1.0_r_def

    ! Coordinate basis
    diff_basis_wx(:,1) = (/ -1.0_r_def, -1.0_r_def, -1.0_r_def /)
    diff_basis_wx(:,2) = (/  1.0_r_def, -1.0_r_def, -1.0_r_def /)
    diff_basis_wx(:,3) = (/  1.0_r_def,  1.0_r_def, -1.0_r_def /)
    diff_basis_wx(:,4) = (/ -1.0_r_def,  1.0_r_def, -1.0_r_def /)
    diff_basis_wx(:,5) = (/ -1.0_r_def, -1.0_r_def,  1.0_r_def /)
    diff_basis_wx(:,6) = (/  1.0_r_def, -1.0_r_def,  1.0_r_def /)
    diff_basis_wx(:,7) = (/  1.0_r_def,  1.0_r_def,  1.0_r_def /)
    diff_basis_wx(:,8) = (/ -1.0_r_def,  1.0_r_def,  1.0_r_def /)
    diff_basis_wx= 0.25_r_def*diff_basis_wx
    basis_wx(:,:) = 0.125_r_def

    ! Fields
    panel_id = (/ 1.0_r_def /)

    ! Compute answer for each direction
    answer = 0.0_r_def
    answer(1,1,1) = 0.5_r_def*dx
    answer(2,1,1) = 0.5_r_def*dx
    answer(3,1,3) = 0.5_r_def*dz
    answer(4,1,3) = 0.5_r_def*dz

    do direction = 1, 3
      projection_operator = 0.0_r_def
      call project_ws_to_w2_operator_code(cell,                    &
                                          nlayers,                 &
                                          ncell,                   &
                                          projection_operator,     &
                                          x, y, z,                 &
                                          panel_id,                &
                                          direction,               &
                                          ndf_w2,                  &
                                          basis_w2,                &
                                          ndf_ws,                  &
                                          basis_ws,                &
                                          ndf_wx, undf_wx, map_wx, &
                                          basis_wx, diff_basis_wx, &
                                          ndf_p, undf_p, map_p,    &
                                          nqp, nqp, wgt, wgt)

      ! P = J*basis_w2 * basis_ws * unit_vector(direction)
      ! where unit_vector is 1 for component direction and zero otherwise
      @assertEqual(answer(:,:,direction), projection_operator(:,:,1), tol)
    end do

  end subroutine test_all

end module project_ws_to_w2_operator_kernel_mod_test
